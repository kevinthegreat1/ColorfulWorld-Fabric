plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.5.1"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name + '-' + project.minecraft_version
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/colorfulworld.accesswidener")
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

//	modLocalRuntime "maven.modrinth:sodium:${project.sodium_version}"

	testImplementation "net.fabricmc:fabric-loader-junit:${project.loader_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

test {
	useJUnitPlatform()
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	file = remapJar.archiveFile
	displayName = "Colorful World ${mod_version} for ${minecraft_version}"
	changelog = rootProject.file("CHANGELOG.md").text
	type = ALPHA
	modLoaders.add("fabric")

	curseforge {
		projectId = "714920"
		projectSlug = "colorful-world" // Required for discord webhook
		accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
		minecraftVersions.add("1.20.3")
		minecraftVersions.add("1.20.4")
		requires("fabric-api")
	}
	modrinth {
		projectId = "9053RcP8"
		accessToken = providers.environmentVariable("MODRINTH_TOKEN")
		minecraftVersions.add("1.20.3")
		minecraftVersions.add("1.20.4")
	}
	github {
		repository = "kevinthegreat1/ColorfulWorld-Fabric"
		accessToken = providers.environmentVariable("GITHUB_TOKEN")
		commitish = "main"
	}
}
